apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'sonar-runner'

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.9'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

archivesBaseName = 'theta'
version = rootProject.version

test {
    useJUnit {
        // IntegrationTest is manual execution-only.
        // Because these tests requires connection with real RICOH THETA.
        // "integrationTest" task executes these tests.
        excludeCategories 'com.theta360.test.categories.IntegrationTest'
    }
}

javadoc {
    mustRunAfter clean
}

jar {
    mustRunAfter clean
}

sonarRunner {
    sonarProperties {
        property "sonar.projectName", archivesBaseName
        property "sonar.host.url", "http://shiro.be:9000"
        property "sonar.jdbc.url", "jdbc:mysql://shiro.be:3306/sonar"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jdbc.username", java.lang.System.getenv()['SONAR_DB_USER']
        property "sonar.jdbc.password", java.lang.System.getenv()['SONAR_DB_PASSWORD']
    }
}

task integrationTest(type: Test) {
    useJUnit {
        excludeCategories 'com.theta360.test.categories.UnitTest'
        includeCategories 'com.theta360.test.categories.IntegrationTest'
    }
}

task javadocArchive(type: Zip, dependsOn: [clean, javadoc]) {
    mustRunAfter clean

    baseName "javadoc-$archivesBaseName"
    from javadoc.destinationDir
}

task distribute(type: Copy, dependsOn: [clean, jar, javadocArchive]) {
    mustRunAfter clean

    from libsDir
    into distsDir
}
