apply plugin: 'java'
apply plugin: 'findbugs'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

archivesBaseName = 'theta'
version = '0.1.0'

repositories {
    mavenCentral()
}

test {
    useJUnit {
        // IntegrationTest is manual execution-only.
        // Because these tests requires connection with real RICOH THETA.
        // "integrationTest" task executes these tests.
        excludeCategories 'com.theta360.test.categories.IntegrationTest'
    }
}

findbugs {
    effort = 'max'
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.9'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jar {
    mustRunAfter clean
}

task integrationTest(type: Test) {
    useJUnit {
        excludeCategories 'com.theta360.test.categories.UnitTest'
        includeCategories 'com.theta360.test.categories.IntegrationTest'
    }
}

task distribute(type: Copy, dependsOn: [clean, jar]) {
    from "build/libs"
    into "build/bin"
}
