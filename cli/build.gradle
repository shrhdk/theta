import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'findbugs'

dependencies {
    compile project(':lib')
    compile 'commons-cli:commons-cli:1.2'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.9'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

archivesBaseName = 'theta-cli'
version = rootProject.version

jar {
    mustRunAfter clean

    manifest {
        attributes 'Implementation-Title': 'RICOH Theta CLI tool'
        attributes 'Implementation-Version': version
        attributes "Main-Class": "com.theta360.cli.ThetaCLI"
    }

    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

task scripts(type: Copy) {
    mustRunAfter clean

    from 'src/main/script'
    into 'build/bin'
    filter(ReplaceTokens, tokens: [ARCHIVE_BASE_NAME: archivesBaseName, VERSION: version])
}

task distribute(type: Copy, dependsOn: [clean, jar, scripts]) {
    from "build/libs"
    into "build/bin"
}
